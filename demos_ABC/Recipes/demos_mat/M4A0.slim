// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 9240);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p34", 27605, p0);
	sim.addSubpopSplit("p12", 40673, p0);
	p0.setSubpopulationSize(0);
}


427316 {
	sim.addSubpopSplit("p3", 12799, p34);
	sim.addSubpopSplit("p4", 5173, p34);
	p34.setSubpopulationSize(0);
	p12.setMigrationRates(c(p3), c(0.04));
	p12.setMigrationRates(c(p4), c(0.055));
	p3.setMigrationRates(c(p4), c(0.066));
}


922886 {
	sim.addSubpopSplit("p1", 9665, p12);
	sim.addSubpopSplit("p2", 69659, p12);
	p12.setSubpopulationSize(0);
	p1.setMigrationRates(c(p2), c(0.096));
	p1.setMigrationRates(c(p3), c(0.086));
	p1.setMigrationRates(c(p4), c(0.042));
	p2.setMigrationRates(c(p3), c(0.109));
	p2.setMigrationRates(c(p4), c(0.064));
}

1440354 late() {

    g = c();
    pops= 0:3;
    samples= c(500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
