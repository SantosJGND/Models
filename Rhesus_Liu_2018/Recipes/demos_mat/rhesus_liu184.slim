// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 795842);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 26249, p0);
	sim.addSubpopSplit("p1234", 245531, p0);
	p0.setSubpopulationSize(0);
}


64125 {
	sim.addSubpopSplit("p4", 39782, p1234);
	sim.addSubpopSplit("p123", 3721, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.6685674740585254e-05));
	p4.setMigrationRates(c(p5), c(9.565826683542438e-06));
}


75184 {
	sim.addSubpopSplit("p3", 9243, p123);
	sim.addSubpopSplit("p12", 44294, p123);
	p123.setSubpopulationSize(0);
}


81719 {
	sim.addSubpopSplit("p1", 1146, p12);
	sim.addSubpopSplit("p2", 4784, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.618867342577716e-05));
	p4.setMigrationRates(c(p1), c(1.0899986052885089e-05));
	p4.setMigrationRates(c(p2), c(1.1569168181569264e-05));
	p1.setMigrationRates(c(p2), c(8.066936642829158e-06));
	p1.setMigrationRates(c(p4), c(2.7527271021042598e-06));
	p2.setMigrationRates(c(p1), c(0.0005141700592328274));
	p2.setMigrationRates(c(p3), c(8.44158377421559e-05));
	p2.setMigrationRates(c(p4), c(2.997471503464449e-05));
	p2.setMigrationRates(c(p5), c(2.219487165603809e-05));
}

84811 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
