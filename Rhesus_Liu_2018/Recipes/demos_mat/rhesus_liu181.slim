// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 802122);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 24597, p0);
	sim.addSubpopSplit("p1234", 237433, p0);
	p0.setSubpopulationSize(0);
}


63622 {
	sim.addSubpopSplit("p4", 39433, p1234);
	sim.addSubpopSplit("p123", 1890, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.227292442440744e-05));
	p4.setMigrationRates(c(p5), c(9.163492277691694e-06));
}


75123 {
	sim.addSubpopSplit("p3", 10602, p123);
	sim.addSubpopSplit("p12", 53309, p123);
	p123.setSubpopulationSize(0);
}


81393 {
	sim.addSubpopSplit("p1", 1228, p12);
	sim.addSubpopSplit("p2", 5126, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.6167022230626013e-05));
	p4.setMigrationRates(c(p1), c(9.421300952107395e-06));
	p4.setMigrationRates(c(p2), c(1.2652620255064603e-05));
	p1.setMigrationRates(c(p2), c(8.300060058163162e-06));
	p1.setMigrationRates(c(p4), c(2.8449762575144468e-06));
	p2.setMigrationRates(c(p1), c(0.0005114856767803498));
	p2.setMigrationRates(c(p3), c(7.999892913113221e-05));
	p2.setMigrationRates(c(p4), c(3.335973909313646e-05));
	p2.setMigrationRates(c(p5), c(2.068656797067609e-05));
}

84913 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
