// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 799262);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 25909, p0);
	sim.addSubpopSplit("p1234", 253660, p0);
	p0.setSubpopulationSize(0);
}


63603 {
	sim.addSubpopSplit("p4", 39979, p1234);
	sim.addSubpopSplit("p123", 1484, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.240417331829724e-05));
	p4.setMigrationRates(c(p5), c(9.647000832835263e-06));
}


73912 {
	sim.addSubpopSplit("p3", 8877, p123);
	sim.addSubpopSplit("p12", 48084, p123);
	p123.setSubpopulationSize(0);
}


80054 {
	sim.addSubpopSplit("p1", 1191, p12);
	sim.addSubpopSplit("p2", 4760, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.6846991153845247e-05));
	p4.setMigrationRates(c(p1), c(1.0609439340986837e-05));
	p4.setMigrationRates(c(p2), c(1.2592695326514572e-05));
	p1.setMigrationRates(c(p2), c(7.381112253270224e-06));
	p1.setMigrationRates(c(p4), c(2.755822766277157e-06));
	p2.setMigrationRates(c(p1), c(0.0005310313210980134));
	p2.setMigrationRates(c(p3), c(7.941668138920023e-05));
	p2.setMigrationRates(c(p4), c(3.13182923928125e-05));
	p2.setMigrationRates(c(p5), c(2.0130723865409916e-05));
}

83047 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
