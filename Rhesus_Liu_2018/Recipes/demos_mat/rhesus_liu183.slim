// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 802331);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 26691, p0);
	sim.addSubpopSplit("p1234", 232103, p0);
	p0.setSubpopulationSize(0);
}


63518 {
	sim.addSubpopSplit("p4", 38024, p1234);
	sim.addSubpopSplit("p123", 1759, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.637187855826587e-05));
	p4.setMigrationRates(c(p5), c(9.08703989740014e-06));
}


74645 {
	sim.addSubpopSplit("p3", 8467, p123);
	sim.addSubpopSplit("p12", 48717, p123);
	p123.setSubpopulationSize(0);
}


81335 {
	sim.addSubpopSplit("p1", 1200, p12);
	sim.addSubpopSplit("p2", 4866, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.498721462054023e-05));
	p4.setMigrationRates(c(p1), c(1.030153421525911e-05));
	p4.setMigrationRates(c(p2), c(1.2334267098569647e-05));
	p1.setMigrationRates(c(p2), c(7.866716806106654e-06));
	p1.setMigrationRates(c(p4), c(2.8665377695070776e-06));
	p2.setMigrationRates(c(p1), c(0.0005150597965626312));
	p2.setMigrationRates(c(p3), c(8.231262419927538e-05));
	p2.setMigrationRates(c(p4), c(3.261382135392964e-05));
	p2.setMigrationRates(c(p5), c(2.618916978251929e-05));
}

84397 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
