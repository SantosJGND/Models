// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 801937);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 27914, p0);
	sim.addSubpopSplit("p1234", 238168, p0);
	p0.setSubpopulationSize(0);
}


63514 {
	sim.addSubpopSplit("p4", 40501, p1234);
	sim.addSubpopSplit("p123", 18149, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.9187080830899904e-05));
	p4.setMigrationRates(c(p5), c(9.014114563186522e-06));
}


73974 {
	sim.addSubpopSplit("p3", 10698, p123);
	sim.addSubpopSplit("p12", 40427, p123);
	p123.setSubpopulationSize(0);
}


80240 {
	sim.addSubpopSplit("p1", 1213, p12);
	sim.addSubpopSplit("p2", 4431, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.8017395459071424e-05));
	p4.setMigrationRates(c(p1), c(1.0350625990795334e-05));
	p4.setMigrationRates(c(p2), c(1.2980434289228863e-05));
	p1.setMigrationRates(c(p2), c(7.488507947140976e-06));
	p1.setMigrationRates(c(p4), c(2.8351783490624828e-06));
	p2.setMigrationRates(c(p1), c(0.0005132950720949399));
	p2.setMigrationRates(c(p3), c(8.47375469700636e-05));
	p2.setMigrationRates(c(p4), c(3.7397120603962236e-05));
	p2.setMigrationRates(c(p5), c(2.4174714702662506e-05));
}

83217 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
