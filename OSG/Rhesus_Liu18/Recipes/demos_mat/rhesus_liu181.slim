// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 4161);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 25398, p0);
	sim.addSubpopSplit("p1234", 799142, p0);
	p0.setSubpopulationSize(0);
}


63273 {
	sim.addSubpopSplit("p4", 38441, p1234);
	sim.addSubpopSplit("p123", 57109, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.464384264280315e-05));
	p4.setMigrationRates(c(p5), c(9.71640852987763e-06));
}


74742 {
	sim.addSubpopSplit("p3", 10181, p123);
	sim.addSubpopSplit("p12", 208712, p123);
	p123.setSubpopulationSize(0);
}


81716 {
	sim.addSubpopSplit("p1", 1130, p12);
	sim.addSubpopSplit("p2", 4871, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.6029098963656884e-05));
	p4.setMigrationRates(c(p1), c(1.0115405921173938e-05));
	p4.setMigrationRates(c(p2), c(1.2724376591481148e-05));
	p1.setMigrationRates(c(p2), c(7.2722931273929335e-06));
	p1.setMigrationRates(c(p4), c(2.955877598949014e-06));
	p2.setMigrationRates(c(p1), c(0.0005136826460334489));
	p2.setMigrationRates(c(p3), c(7.678552757948894e-05));
	p2.setMigrationRates(c(p4), c(3.508855189802986e-05));
	p2.setMigrationRates(c(p5), c(2.060587310453448e-05));
}

85143 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
