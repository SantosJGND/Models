// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 16592);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 24849, p0);
	sim.addSubpopSplit("p1234", 809322, p0);
	p0.setSubpopulationSize(0);
}


63511 {
	sim.addSubpopSplit("p4", 36064, p1234);
	sim.addSubpopSplit("p123", 42393, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.4756644545045466e-05));
	p4.setMigrationRates(c(p5), c(9.574437941490184e-06));
}


74106 {
	sim.addSubpopSplit("p3", 9602, p123);
	sim.addSubpopSplit("p12", 241109, p123);
	p123.setSubpopulationSize(0);
}


80184 {
	sim.addSubpopSplit("p1", 1177, p12);
	sim.addSubpopSplit("p2", 2957, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.6461557697150626e-05));
	p4.setMigrationRates(c(p1), c(1.0052449542170695e-05));
	p4.setMigrationRates(c(p2), c(1.3352913787513678e-05));
	p1.setMigrationRates(c(p2), c(8.400406859637458e-06));
	p1.setMigrationRates(c(p4), c(2.8508312930582164e-06));
	p2.setMigrationRates(c(p1), c(0.000524126634918199));
	p2.setMigrationRates(c(p3), c(8.337946520151787e-05));
	p2.setMigrationRates(c(p4), c(3.739155767189139e-05));
	p2.setMigrationRates(c(p5), c(1.9258166255770155e-05));
}

83680 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
