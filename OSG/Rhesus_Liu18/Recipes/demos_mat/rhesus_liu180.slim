// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 17093);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 25667, p0);
	sim.addSubpopSplit("p1234", 808467, p0);
	p0.setSubpopulationSize(0);
}


63465 {
	sim.addSubpopSplit("p4", 39743, p1234);
	sim.addSubpopSplit("p123", 55995, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.5760875963494674e-05));
	p4.setMigrationRates(c(p5), c(9.47409882904449e-06));
}


74360 {
	sim.addSubpopSplit("p3", 11260, p123);
	sim.addSubpopSplit("p12", 282762, p123);
	p123.setSubpopulationSize(0);
}


80318 {
	sim.addSubpopSplit("p1", 1121, p12);
	sim.addSubpopSplit("p2", 4846, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.669165165958226e-05));
	p4.setMigrationRates(c(p1), c(1.0373112969745362e-05));
	p4.setMigrationRates(c(p2), c(1.2589625628536411e-05));
	p1.setMigrationRates(c(p2), c(7.628886993936971e-06));
	p1.setMigrationRates(c(p4), c(2.839111753188522e-06));
	p2.setMigrationRates(c(p1), c(0.0005179509250134209));
	p2.setMigrationRates(c(p3), c(8.237516299260209e-05));
	p2.setMigrationRates(c(p4), c(3.147597100342759e-05));
	p2.setMigrationRates(c(p5), c(2.0189492386886133e-05));
}

83731 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
