// this version of the recipe is meant to be passed arguments:
// - fasta_file;
// - vcf_file;
// - mu: mutation rate total;
// - mut_file: mutation to read file from. 
//initializeMutationRate(2.36e-8);
initialize() {
	initializeSLiMOptions(nucleotideBased=T);
	defineConstant("L", initializeAncestralNucleotides(fasta_file));
	initializeMutationTypeNuc("m1", 0.5, "f", 0.0);

	mm = mm16To256(mmJukesCantor(mu / 3));
	//mut_file
	file_mut= readFile(mut_file);
	for (line in file_mut)
	{
		line=strsplit(line,sep= '\t');
		mut=line[0];
		catn(mut);
		mut_idx= nucleotidesToCodons(mut);
		rates= strsplit(line[1],sep= ',');
		for (r in 0:3)
		{
			mm[mut_idx,r]= asFloat(rates[r]);
		}
	}

	initializeGenomicElementType("g1", m1, 1.0, mutationMatrix=mm);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(1e-8);
}

1 {

    sim.addSubpop("p0", 802818);
    c = sim.chromosome;
    catn("Ancestral: " + paste(c.ancestralNucleotides(format="char")[0:20],sep=""));
    catn();
}


50000 {
	sim.addSubpopSplit("p5", 25692, p0);
	sim.addSubpopSplit("p1234", 227989, p0);
	p0.setSubpopulationSize(0);
}


62935 {
	sim.addSubpopSplit("p4", 35062, p1234);
	sim.addSubpopSplit("p123", 7519, p1234);
	p1234.setSubpopulationSize(0);
	p5.setMigrationRates(c(p4), c(4.842839061907508e-05));
	p4.setMigrationRates(c(p5), c(9.156439523338676e-06));
}


73965 {
	sim.addSubpopSplit("p3", 9656, p123);
	sim.addSubpopSplit("p12", 58448, p123);
	p123.setSubpopulationSize(0);
}


79802 {
	sim.addSubpopSplit("p1", 1154, p12);
	sim.addSubpopSplit("p2", 4350, p12);
	p12.setSubpopulationSize(0);
	p5.setMigrationRates(c(p2), c(2.5795022518324817e-05));
	p4.setMigrationRates(c(p1), c(1.0653460568785022e-05));
	p4.setMigrationRates(c(p2), c(1.2072590077133891e-05));
	p1.setMigrationRates(c(p2), c(8.035801727447468e-06));
	p1.setMigrationRates(c(p4), c(2.852304232102561e-06));
	p2.setMigrationRates(c(p1), c(0.0005290283369378988));
	p2.setMigrationRates(c(p3), c(7.876497347414355e-05));
	p2.setMigrationRates(c(p4), c(3.2858054211162065e-05));
	p2.setMigrationRates(c(p5), c(2.369898550885287e-05));
}

83303 late() {

    g = c();
    pops= 0:4;
    samples= c(500,500,500,500,500);
    for (x in pops) 
        g= c(g, sim.subpopulations[x].sampleIndividuals(samples[x]).genomes);

    g.outputVCF(vcf_file,simplifyNucleotides=T);
    
}
